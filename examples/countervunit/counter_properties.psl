vunit counter_properties (counter) {

    -- Default clock for PSL assertions
    default clock is rising_edge(clk);

    -- Assert we do not go over the maximum
    never_go_over_the_maximum: assert always (Q <= MAX_COUNT);

    -- Assert the count is cleared on reset
    reset_clears_count: assert always ({rst = '1'} |=> {Q = 0});

    -- Assert the count increments when not at maximum
    -- Note that `abort rst` at the end of the assertion avoids checking
    -- the property when reset is active
    count_increments: assert always ({Q /= MAX_COUNT} |=> {Q = prev(Q) + 1}) abort rst;

    -- Assert the count overflows when at maximum
    count_overflow: assert always ({Q = MAX_COUNT} |=> {Q = 0}) abort rst;

    -- Cover the overflow -> zero case
    -- What we write here has to be a sequence inside curly braces {}, even if
    -- we only have one element (for example, { Q = MAX_COUNT }
    cover_overflow_to_zero: cover {Q = MAX_COUNT ; Q = 0};
    cover_overflow_to_zero_and_reset: cover {Q = MAX_COUNT ; Q = 0; [*]; rst = '1'; [*]; Q = MAX_COUNT ; Q = 0};

    -- Force a reset in the first clock cycle
    -- Since we don't have an 'always', this assumption only applies to the
    -- first clock cycle
    assume_initial_reset: assume rst = '1';

}

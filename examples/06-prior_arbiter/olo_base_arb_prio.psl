vunit olo_base_arb_prio (olo_base_arb_prio) {

    -- Default clock for PSL assertions
    default clock is rising_edge(Clk);

    -- Force a reset in the first clock cycle
    assume_initial_reset: assume Rst = '1';

            sequence transmission is {
                In_Req = "011" and Out_Grant = "010";
                In_Req = "101" and Out_Grant = "100";
                In_Req = "101" and Out_Grant = "100";
                In_Req = "001" and Out_Grant = "001";
                In_Req = "000" and Out_Grant = "000";
                In_Req = "011" and Out_Grant = "010";
                In_Req = "001" and Out_Grant = "001";
                In_Req = "000" and Out_Grant = "000"
            };

            assert always onehot0(Out_Grant);
            assert always {countones(In_Req) = 0} |-> {countones(Out_Grant) = 0} abort Rst;
            assert always {countones(In_Req) >= 1} |-> {onehot(Out_Grant)};

            assert always ({In_Req = "000"} |-> {Out_Grant = "000"}) abort Rst;
            assert always ({In_Req = "001"} |-> {Out_Grant = "001"}) abort Rst;
            assert always ({In_Req = "010"} |-> {Out_Grant = "010"}) abort Rst;
            assert always ({In_Req = "011"} |-> {Out_Grant = "010"}) abort Rst;
            assert always ({In_Req = "100"} |-> {Out_Grant = "100"}) abort Rst;
            assert always ({In_Req = "101"} |-> {Out_Grant = "100"}) abort Rst;
            assert always ({In_Req = "110"} |-> {Out_Grant = "100"}) abort Rst;
            assert always ({In_Req = "111"} |-> {Out_Grant = "100"}) abort Rst;

            cover {countones(In_Req) = 3};
            cover {transmission};

}
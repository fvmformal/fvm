vunit uart_tx(uart_tx(Behavioral)) {
  inherit uart_tx_sequence;

  -- Default clock for PSL assertions
  default clock is rising_edge(clk);

  -- Assume a reset on the first clock cycle
  assume_initial_reset: assume rst = '1';

  -- Assert the UART never reads from an empty FIFO
  never_read_from_empty_fifo:
    assert never (rd_en and empty);

  -- If we have a rd_en, we expect a transmission, unless we receive a reset
  correct_tx_after_rd_en:
    assert always (({rd_en} |=> {transmission(data)}) abort rst);

  -- Symbolic constant: Force the solver to make data constant in time, but it can
  -- choose any value it wants. This works because the solver may chose any
  -- for data at clock cycle zero, and then this assumption forces data to
  -- remain constant for the rest of time
  data_symbolic_constant:
    assume always ((data = prev(data)) abort rst);
    --assume always (data = x"AA"); --(data = prev(data));

  -- Cover a number of transmissions
  cover_two_transmisions_two_cycles_between_them:
    cover {transmission(data); true[*2]; transmission(data)};

  cover_two_transmisions_any_cycles_between_them:
    cover {transmission(data); true[*]; transmission(data)};
  
  cover_one_transmission:
    cover transmission(data);

  -- We can define sequences reusing other sequences, which is helpful to
  -- construct more complex sequences
  sequence transmission_and_rest is {
    transmission(data); true[*2]; true[*]
  };

  -- Change the number below to the number of transmissions you want to see in
  -- your cover trace
  cover_more_than_two_transmissions:
    cover {transmission_and_rest[*3]};

}

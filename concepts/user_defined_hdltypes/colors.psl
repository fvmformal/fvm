-- Copyright 2024-2026 Universidad de Sevilla
-- SPDX-License-Identifier: Apache-2.0

vunit colors (colors(behavioral)) {

    -- Default clock for PSL assertions
    default clock is rising_edge(clk);

    -- Make sure the output vector is one-hot
    colorvect_is_onehot:
      assert always onehot(colorvect);

    -- Define a sequence that uses a custom hdltype
    sequence transition (
      hdltype color_t color1;
      hdltype color_t color2
    ) is {
      color = color1; color = color2
    };

    -- Define a sequence that uses a custom hdltype of type record
    sequence two_transitions (
      hdltype color_record colors
    ) is {
      color = colors.a; color = colors.b; color = colors.c
    };

    -- Ensure we see specific transitions
    cover_violet_to_red:
      cover {transition(violet, red)};

    cover_yellow_to_green:
      cover transition(yellow, green);

    cover_red_orange_yellow:
      cover two_transitions((a => red, b => orange, c => yellow));

    -- Assert we don't see some illegal transitions
    -- (of course there are more transitions that would be illegal)
    never_blue_to_yellow:
      assert never transition(blue, yellow);

    never_indigo_to_orange:
      assert never transition(indigo, orange);

    -- Since red is the reset state, we can always go to it if we assert the
    -- reset input, so if we don't add the "abort rst" the assertion will fire
    never_yellow_to_red:
      assert (never transition(yellow, red)) abort rst;
}
